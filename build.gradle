plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin'      version '0.0.8'
    id 'org.beryx.jlink'               version '2.15.1'
    id 'com.github.ben-manes.versions' version '0.23.0'
}

// Application Configuration
ext {
    appName = "localendar"
    appLauncher = "localendar"
    appPackaging = "localendar"
    appModuleName = "edu.buffalo.cse442.nullterminators"
    appMainClass = "edu.buffalo.cse442.nullterminators.GUI"
}

mainClassName = "${ext.appModuleName}/${ext.appMainClass}"

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.28.0'
}

javafx {
    version = '12'
    modules = [
       'javafx.base',
       'javafx.graphics',
       'javafx.controls',
       'javafx.fxml',
       'javafx.web'
    ]
}

jlink {
    imageZip = project.file("${buildDir}/distributions/${appPackaging}-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = "${appLauncher}"
    }
}

task dist {
    dependsOn clean, jlinkZip
    description "Calls clean and then jlinkZip [default]"
}

defaultTasks 'dist'

if (project.hasProperty('debugRun') && project.debugRun as boolean) {
    run {
        jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=*:5005'
    }
}